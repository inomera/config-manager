plugins {
    id 'java-library'
}

apply from: "${rootProject.rootDir}/gradle/tests.gradle"
apply from: "${rootProject.rootDir}/gradle/publishing.gradle"

group = 'com.inomera.telco.commons'
version = rootProject.file('VERSION').text.trim()
sourceCompatibility = '1.8'
targetCompatibility = 1.8

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    testImplementation {
        extendsFrom compileOnly
    }
}

task copyConfigurationMetadata(type: Copy) {
    from(compileJava) {
        include 'META-INF/spring-configuration-metadata.json'
    }
    into "out/production/classes"
}

compileJava {
    dependsOn processResources
    finalizedBy copyConfigurationMetadata
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter:2.1.7.RELEASE'
    implementation 'org.springframework.boot:spring-boot-autoconfigure:2.1.7.RELEASE'
    implementation 'org.springframework.boot:spring-boot:2.1.7.RELEASE'

    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:2.1.7.RELEASE'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    api 'com.inomera.telco.commons:lock-provider:1.2.0'
    api project(':config-manager-core')

    // Cassandra
    compileOnly 'com.datastax.cassandra:cassandra-driver-core:3.6.0'
    compileOnly 'com.datastax.cassandra:cassandra-driver-mapping:3.6.0'
    compileOnly 'com.datastax.cassandra:cassandra-driver-extras:3.6.0'

    // Mongo
    compileOnly 'org.mongodb:mongodb-driver:3.11.2'

    testImplementation('org.junit.jupiter:junit-jupiter-api:5.2.0')
    testCompile('org.junit.jupiter:junit-jupiter-params:5.2.0')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.2.0')
    testCompile "org.mockito:mockito-core:2.23.4"
    testCompile('org.mockito:mockito-junit-jupiter:2.18.3')
    testCompile('org.hamcrest:hamcrest-all:1.3')
    testCompile('com.h2database:h2:1.4.196')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "${project.group}"
            artifactId = "${project.name}"
            version = "${project.version}"

            from components.java
            artifact tasks.sourceJar
        }
    }
}
