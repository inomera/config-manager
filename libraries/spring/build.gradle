plugins {
    id 'java-library'
}

apply from: "${rootProject.rootDir}/gradle/tests.gradle"
apply from: "${rootProject.rootDir}/gradle/publishing.gradle"

group = 'com.inomera.telco.commons'
version = rootProject.file('VERSION').text.trim()
sourceCompatibility = JavaVersion.VERSION_23
targetCompatibility = JavaVersion.VERSION_23

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    testImplementation {
        extendsFrom compileOnly
    }
}

tasks.register('copyConfigurationMetadata', Copy) {
    from(compileJava) {
        include 'META-INF/spring-configuration-metadata.json'
    }
    into "out/production/classes"
}

compileJava {
    dependsOn processResources
    finalizedBy copyConfigurationMetadata
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter:${versions.spring}"
    implementation "org.springframework.boot:spring-boot-autoconfigure:${versions.spring}"
    implementation "org.springframework.boot:spring-boot:${versions.spring}"

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${versions.spring}"
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    // Use for production
    //api 'com.inomera.telco.commons:config-manager-core:5.0.0'
    // Use for local development and testing
    api project(':config-manager-core')

    // Cassandra
    compileOnly "com.datastax.oss:java-driver-core:${versions.cassandra}"
    compileOnly "com.datastax.oss:java-driver-mapper-processor:${versions.cassandra}"

    // Mongo
    compileOnly "org.mongodb:mongodb-driver-sync:${versions.mongodb}"
    compileOnly "org.mongodb:mongodb-driver-core:${versions.mongodb}"

    // Redis
    compileOnly "org.redisson:redisson:${versions.redisson}"

    // Jasypt Decryption
    compileOnly 'org.jasypt:jasypt:1.9.3'

    testImplementation('org.junit.jupiter:junit-jupiter-api:5.11.4')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.11.4')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.11.4')
    testImplementation "org.mockito:mockito-core:5.15.2"
    testImplementation('org.mockito:mockito-junit-jupiter:5.15.2')
    testImplementation('org.hamcrest:hamcrest:3.0')
    testImplementation("com.h2database:h2:${versions.h2}")

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "${project.group}"
            artifactId = "${project.name}"
            version = "${project.version}"

            from components.java
            artifact tasks.sourceJar
        }
    }
}

tasks.generatePomFileForMavenJavaPublication {
    destination = file("${project.projectDir}/build/publications/mavenJava/${project.name}-${project.version}.pom")
}
